apply plugin: "com.jfrog.bintray"
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven-publish'

def siteUrl = 'https://github.com/brettywhite/cryptoMessenger'      // Homepage URL of the library
def gitUrl = 'https://github.com/brettywhite/cryptoMessenger.git'   // Git repository URL
group = "com.brettywhite" // Maven Group ID for the artifact
def libDescription = 'cryptoMessenger mobile library for Android'
version='0.0.1'
install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'com.brettywhite:cryptoMessenger'
                description = libDescription
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'Apache-2.0'
                        url 'https://opensource.org/licenses/Apache-2.0'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

bintray {
    Properties props = new Properties()
    props.load(new FileInputStream("$projectDir/bintray.properties"))

    // Authorization
    user = props.getProperty("bintray.user")
    key = props.getProperty("bintray.key")

    configurations = ['archives']
    pkg {
        repo = props.getProperty("bintray.repo")
        name = props.getProperty("bintray.artifact")
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        userOrg = props.getProperty("bintray.userorg")
        licenses = ["Apache-2.0"]
        publish = props.getProperty("bintray.publish")  // Will upload to jCenter
        version {
            name = props.getProperty("bintray.version")  // Change to release version
            desc = libDescription
            released  = new Date()  // Will be the current date & time
            vcsTag = props.getProperty("bintray.vcs") // Should match git tag
        }
    }
}